managed with additional save implementation in class zhrd_cl_bp_leave_save unique; // implementation in class zbp_hrt_i_leave unique;
strict ( 2 );

define behavior for zhrd_I_LEAVE //alias <alias_name>
implementation in class zhrd_cl_bp_leave unique
persistent table zhrd_leave
lock master
authorization master ( instance )
etag master LastChangedAt
{
  create;
  update;
  delete;
  field ( readonly ) LeaveGuid;
  field ( numbering : managed ) LeaveGuid;
  association _Contacts { create (features : instance); }

  action suggestBackup result[1] $self;
  factory action (features : instance) copyLeave[1];

  validation validateDates on save { field DateTo; field DateFrom;  }

  determination determineDateFrom on save { field DateTo; }

  determination determineDateFrom2 on modify { create; field DateTo; }


  mapping for zhrd_leave
    {
      LeaveGuid        = leave_guid;
      RequestorId      = requestor_id;
      LeaveTypeId      = leave_type_id;
      Status           = status;
      DateFrom         = date_from;
      DateTo           = date_to;
      BackupId         = backup_id;
      RequestorComment = requestor_comment;
      ApproverId       = approver_id;
      ApproverComment  = approver_comment;
      CreatedBy        = created_by;
      CreatedAt        = created_at;
      LastChangedBy    = last_changed_by;
      LastChangedAt    = last_changed_at;
    }
}

define behavior for zhrd_I_LEAVE_CONT //alias <alias_name>
implementation in class zhrd_cl_bp_contact unique
persistent table zhrd_leave_cont
lock dependent by _Leave
authorization dependent by _Leave
etag dependent
early numbering
{
  update;
  delete;
  field ( readonly ) LeaveGuid, ContactNr;
  association _Leave;

  mapping for zhrd_leave_cont
    {
      LeaveGuid = LEAVE_GUID;
      ContactNr = CONTACT_NR;
      FullName  = FULL_NAME;
      Email     = EMAIL;
    }

}